#--------------------------------------
# Navigation & Editing
#--------------------------------------

# Set Vi keybindings for command-line editing.
# bindkey -v
# export KEYTIMEOUT=1 # Reduces delay for escape key in terminal.

# Use vim to edit long commands by pressing 'v' in normal mode.
# autoload -Uz edit-command-line
# zle -N edit-command-line
# bindkey -M vicmd v edit-command-line

# Set Emacs keybindings
bindkey -e

# --- Directory and Globbing Options ---
setopt AUTO_CD              # Go to a directory by typing its name.
setopt AUTO_PUSHD           # Automatically push directories onto the stack.
setopt PUSHD_IGNORE_DUPS    # Don't push duplicate directories to the stack.
setopt PUSHD_SILENT         # Suppress printing the directory stack.
setopt CDABLE_VARS          # Allow cd'ing into a variable representing a path.
setopt EXTENDED_GLOB        # Enable more advanced globbing features.
setopt CORRECT              # Enable spelling correction for commands.

#--------------------------------------
# Shell State & Caching
#--------------------------------------

# Checks for XDG Base Directory paths for history and cache files.
[ -d "$XDG_CACHE_HOME/zsh" ] || mkdir -p "$XDG_CACHE_HOME/zsh"
[ -d "$XDG_STATE_HOME/zsh" ] || mkdir -p "$XDG_STATE_HOME/zsh"
[ -d "$XDG_STATE_HOME/less" ] || mkdir -p "$XDG_STATE_HOME/less"
[ -d "$XDG_STATE_HOME/R" ] || mkdir -p "$XDG_STATE_HOME/R"
[ -d "$XDG_STATE_HOME/python" ] || mkdir -p "$XDG_STATE_HOME/python"

HISTFILE="$XDG_STATE_HOME/zsh/history"

zstyle ':completion:*' cache-path "$XDG_CACHE_HOME/zsh/zcompcache"
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}' # Case-insensitive tab completion.

# Initialize the completion system only if it hasn't been already.
autoload -Uz compinit
if [[ -z "$_comp_dumpfile" ]]; then
  compinit -d "$XDG_CACHE_HOME/zsh/zcompdump-$ZSH_VERSION"
fi

#--------------------------------------
# History
#--------------------------------------

export HISTSIZE=10000             # Max events stored in current session.
export SAVEHIST=10000             # Max events saved to the history file.

setopt EXTENDED_HISTORY           # Save timestamp and duration for each command.
setopt SHARE_HISTORY              # Share history across all open shells instantly.
setopt HIST_EXPIRE_DUPS_FIRST     # When trimming history, remove duplicates first.
setopt HIST_IGNORE_ALL_DUPS       # If a new command is a duplicate, remove the old one.
setopt HIST_FIND_NO_DUPS          # When searching, don't show duplicate entries.
setopt HIST_IGNORE_SPACE          # Don't save commands that start with a space.
setopt HIST_SAVE_NO_DUPS          # Don't write duplicate events to the history file.
setopt HIST_VERIFY                # Show a command before running it from history.

#--------------------------------------
# Prompt & Appearance
#--------------------------------------

PROMPT="%F{cyan}%n %1~ %# %f"
export CLICOLOR=1
export LSCOLORS=ExFxBxDxCxegedabagacad

#--------------------------------------
# PATH Configuration
#--------------------------------------

typeset -U path # Ensures path entries are unique
path_candidates=(
  /usr/local/opt/ruby/bin # Homebrew Ruby (prepend) for Intel Macs
  "$HOME/.local/bin"
  "$HOME/bin"
  /usr/local/sbin
)
# Only add paths to the array if the directory exists
for dir in $path_candidates; do
  [[ -d "$dir" ]] && path=("$dir" $path)
done
export PATH

# path=(
#   /usr/local/opt/ruby/bin
#   "$HOME/.local/bin"
#   "$HOME/bin"
#   /usr/local/sbin
#   "$path[@]" # Include the existing system path.
# )
# export PATH

#--------------------------------------
# Functions
#--------------------------------------

# Source custom aliases and functions
if [[ -d "$DOTFILES" ]]; then
	source "$DOTFILES/aliases/aliases"
    source "$DOTFILES/zsh/plugins/bd.zsh"
    source "$DOTFILES/zsh/plugins/cursor_mode.zsh"
else
	print -u2 "warning: \$DOTFILES not set or not a directory; skipping custom aliases and functions."
fi

# Source a local aliases file for machine-specific overrides.
if [[ -f "$ZDOTDIR/local_aliases" ]] ; then
	source "$ZDOTDIR/local_aliases"
fi

# Initialize fzf keybindings and completions.
# source <(fzf --zsh)
if command -v fzf >/dev/null; then
  # Static sourcing is faster than source <(fzf --zsh)
  [[ -f "$XDG_CONFIG_HOME/zsh/fzf_setup.zsh" ]] || fzf --zsh > "$XDG_CONFIG_HOME/zsh/fzf_setup.zsh"
  source "$XDG_CONFIG_HOME/zsh/fzf_setup.zsh"
fi

# Source iTerm2 shell integration if it exists.
# test -e "${HOME}/.local/share/iterm2/.iterm2_shell_integration.zsh" && source "${HOME}/.local/share/iterm2/.iterm2_shell_integration.zsh"
[[ -f "${HOME}/.local/share/iterm2/.iterm2_shell_integration.zsh" ]] && \
    source "${HOME}/.local/share/iterm2/.iterm2_shell_integration.zsh"

# yazi shell integration
function y() {
	local tmp="$(mktemp -t "yazi-cwd.XXXXXX")" cwd
	command yazi "$@" --cwd-file="$tmp"
	IFS= read -r -d '' cwd < "$tmp"
	[ "$cwd" != "$PWD" ] && [ -d "$cwd" ] && builtin cd -- "$cwd"
	rm -f -- "$tmp"
}

# --- Custom Functions ---

# cdf [path] → Fuzzy find a directory and cd into it.
# Defaults to searching from the current directory.
cdf() {
  local search_root="${1:-.}"
  local dir
  dir=$(fd --type d --hidden --follow --exclude .git . "$search_root" | fzf) && cd "$dir"
}

# fo [path] → Fuzzy find a file and open it.
# Defaults to searching from the current directory.
fo() {
  local search_path="${1:-.}"
  local file
  file=$(fd --type f . "$search_path" | fzf)

  # Open the file only if a selection was made.
  if [[ -n "$file" ]]; then
    open "$file"
  fi
}

# --- Global Protect VPN Script ---
globalprotect() {
    if [ "$1" = "stop" ]; then
        launchctl bootout gui/$(id -u) /Library/LaunchAgents/com.paloaltonetworks.gp.pangps.plist > /dev/null 2>&1
        launchctl bootout gui/$(id -u) /Library/LaunchAgents/com.paloaltonetworks.gp.pangpa.plist > /dev/null 2>&1
        PID="$(launchctl list | grep palo | cut -f 1)"

        # Kill the processes IDs only if found
        if [ ! -z "$PID" ]; then
                kill -9 $PID
        fi

        echo "VPN unloaded"
    elif [ "$1" = "start" ]; then
        launchctl bootstrap gui/$(id -u) /Library/LaunchAgents/com.paloaltonetworks.gp.pangps.plist
        launchctl bootstrap gui/$(id -u) /Library/LaunchAgents/com.paloaltonetworks.gp.pangpa.plist
        open -a /Applications/GlobalProtect.app
        echo "VPN loaded"
    fi
}
