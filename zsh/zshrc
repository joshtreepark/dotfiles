#--------------------------------------
# Navigation & Editing
#--------------------------------------

# Set Vi keybindings for command-line editing.
bindkey -v
export KEYTIMEOUT=1 # Reduces delay for escape key in terminal.

# Use vim to edit long commands by pressing 'v' in normal mode.
autoload -Uz edit-command-line
zle -N edit-command-line
bindkey -M vicmd v edit-command-line

# --- Directory and Globbing Options ---
setopt AUTO_CD              # Go to a directory by typing its name.
setopt AUTO_PUSHD           # Automatically push directories onto the stack.
setopt PUSHD_IGNORE_DUPS    # Don't push duplicate directories to the stack.
setopt PUSHD_SILENT         # Suppress printing the directory stack.
setopt CDABLE_VARS          # Allow cd'ing into a variable representing a path.
setopt EXTENDED_GLOB        # Enable more advanced globbing features.
setopt CORRECT              # Enable spelling correction for commands.

#--------------------------------------
# Shell State & Caching (XDG)
#--------------------------------------
# Use XDG Base Directory paths for history and completion cache files.

# Ensure the state directory for Zsh exists.
[ -d "$XDG_STATE_HOME/zsh" ] || mkdir -p "$XDG_STATE_HOME/zsh"
HISTFILE="$XDG_STATE_HOME/zsh/history"

# Ensure the cache directory for Zsh exists.
[ -d "$XDG_CACHE_HOME/zsh" ] || mkdir -p "$XDG_CACHE_HOME/zsh"
zstyle ':completion:*' cache-path "$XDG_CACHE_HOME/zsh/zcompcache"
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}' # Case-insensitive tab completion.

# Initialize the completion system only if it hasn't been already.
autoload -Uz compinit
if [[ -z "$_comp_dumpfile" ]]; then
  compinit -d "$XDG_CACHE_HOME/zsh/zcompdump-$ZSH_VERSION"
fi

#--------------------------------------
# History
#--------------------------------------

export HISTSIZE=10000                             # Max events stored in current session.
export SAVEHIST=10000                             # Max events saved to the history file.

setopt EXTENDED_HISTORY          # Save timestamp and duration for each command.
setopt SHARE_HISTORY             # Share history across all open shells instantly.
setopt HIST_EXPIRE_DUPS_FIRST    # When trimming history, remove duplicates first.
setopt HIST_IGNORE_ALL_DUPS      # If a new command is a duplicate, remove the old one.
setopt HIST_FIND_NO_DUPS         # When searching, don't show duplicate entries.
setopt HIST_IGNORE_SPACE         # Don't save commands that start with a space.
setopt HIST_SAVE_NO_DUPS         # Don't write duplicate events to the history file.
setopt HIST_VERIFY               # Show a command before running it from history.

#--------------------------------------
# Aliases
#--------------------------------------

# Source custom aliases from a centralized dotfiles location.
if [[ -n "$DOTFILES" && -d "$DOTFILES" ]]; then
	source "$DOTFILES/aliases/aliases"
else
	print -u2 "warning: \$DOTFILES not set or not a directory; skipping custom aliases."
fi

# Source a local aliases file for machine-specific overrides.
if [[ -f "$ZDOTDIR/local_aliases" ]] ; then
	source "$ZDOTDIR/local_aliases"
fi

#--------------------------------------
# Prompt & Appearance
#--------------------------------------

PROMPT="%F{cyan}%n %1~ %# %f"
export CLICOLOR=1
export LSCOLORS=ExFxBxDxCxegedabagacad

#--------------------------------------
# PATH Configuration
#--------------------------------------

# Prepend user-specific bin directories to the PATH.
path=(
  /usr/local/opt/ruby/bin  # Homebrew Ruby (prepend)
  "$HOME/.local/bin"
  "$HOME/bin"
  /usr/local/sbin
  "$path[@]" # Include the existing system path.
)
export PATH

#--------------------------------------
# Functions & Plugins
#--------------------------------------

# --- Source Plugins ---
if [[ -n "$DOTFILES" && -d "$DOTFILES" ]]; then
    source "$DOTFILES/zsh/plugins/bd.zsh"
    source "$DOTFILES/zsh/plugins/cursor_mode.zsh"
fi

# Initialize fzf keybindings and completions.
source <(fzf --zsh)

# Source iTerm2 shell integration if it exists.
test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"

# --- Custom Functions ---

# cdf [path] → Fuzzy find a directory and cd into it.
# Defaults to searching from the current directory.
cdf() {
  local search_root="${1:-.}"
  local dir
  dir=$(fd --type d --hidden --follow --exclude .git . "$search_root" | fzf) && cd "$dir"
}

# fo [path] → Fuzzy find a file and open it.
# Defaults to searching from the current directory.
fo() {
  local search_path="${1:-.}"
  local file
  file=$(fd --type f . "$search_path" | fzf)

  # Open the file only if a selection was made.
  if [[ -n "$file" ]]; then
    open "$file"
  fi
}
